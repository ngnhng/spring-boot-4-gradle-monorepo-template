// Defines "How" to build.
// It runs after settings.gradle.kts.
// It defines the tasks (compile, test), plugins, dependencies, and repositories.
// In a multi-module project, the root build file acts as an orchestrator to share common logic
// across all sub-projects.

// https://docs.gradle.org/9.3.1/userguide/plugins_intermediate.html#sec:types_of_plugins
plugins {
  // Define the version of the plugin exactly once in the root.
  // In sub-projects, we can now apply the plugin without mentioning the version, ensuring all
  // modules use the exact same Spring Boot version.
  // "Download the plugin and make it available, but do not register any tasks or logic for the root
  // project yet."
  alias(libs.plugins.spring.boot) apply false

  // alias(libs.plugins.spring.dependency.management) apply false

  // Root is technically not a Java project, but this is needed for java toolchain below, ensuring
  // same Java version across sub-projects.
  id("java")

  alias(libs.plugins.spotless) apply false
}

// libs is a special helper generated by Gradle. It works fine in plugins {} and at the top level.
// But when you open subprojects { ... }, the context switches to the Child Project inner scope. The
// Child Project does not have that specific helper script generated for it.
val catalog = libs

allprojects {
  apply(plugin = catalog.plugins.spotless.get().pluginId)
  configure<com.diffplug.gradle.spotless.SpotlessExtension> {
    kotlinGradle {
      target("*.gradle.kts")
      ktfmt()
    }
    format("misc") {
      target("*.md", ".gitignore")
      trimTrailingWhitespace()
      leadingTabsToSpaces(2)
      endWithNewline()
    }
  }
}

subprojects {
  // https://docs.gradle.org/9.3.1/userguide/java_library_plugin.html
  apply(plugin = "java-library")

  // This gives Gradle the ability to read Maven BOMs.
  // Spring has hundreds of dependencies (Jackson, Hibernate, Tomcat, etc.). Instead of manually
  // typing implementation("com.fasterxml.jackson.core:jackson-databind:2.15.0"), we can just type
  // implementation("com.fasterxml.jackson.core:jackson-databind") (no version).
  // This plugin looks at the Spring Boot version we defined in the root and automatically fills in
  // the compatible versions for all these libraries so they don't crash into each other.
  // BUT: The Gradle team and the Spring team are slowly moving away from the
  // io.spring.dependency-management plugin in favor of Gradle's native Platform system.
  // apply(plugin = "io.spring.dependency-management")

  // https://docs.gradle.org/9.3.1/userguide/toolchains.html
  java { toolchain { languageVersion = JavaLanguageVersion.of(21) } }

  // The Gradle team and the Spring team are slowly moving away from the
  // io.spring.dependency-management plugin in favor of Gradle's native Platform system.
}
